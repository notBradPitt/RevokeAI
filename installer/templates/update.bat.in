@echo off
setlocal EnableExtensions EnableDelayedExpansion

PUSHD "%~dp0"

set REVOKE_AI_VERSION=latest
set arg=%1
if "%arg%" neq "" (
   if "%arg:~0,2%" equ "/?" (
       echo Usage: update.bat ^<release name or branch^>
       echo Updates RevokeAI to use the indicated version of the code base.
       echo Find the version or branch for the release you want, and pass it as the argument.
       echo For example '.\update.bat v2.2.5' for release 2.2.5.
       echo             '.\update.bat main' for the latest development version
       echo.
       echo If no argument provided then will install the most recent release, equivalent to
       echo '.\update.bat latest'
       exit /b
   ) else (
       set REVOKE_AI_VERSION=%arg%
   )
)

set REVOKE_AI_SRC="https://github.com/revoke-ai/RevokeAI/archive/!REVOKE_AI_VERSION!.zip"
set REVOKE_AI_DEP=https://raw.githubusercontent.com/revoke-ai/RevokeAI/!REVOKE_AI_VERSION!/environments-and-requirements/requirements-base.txt
set REVOKE_AI_MODELS=https://raw.githubusercontent.com/revoke-ai/RevokeAI/$REVOKE_AI_VERSION/configs/INITIAL_MODELS.yaml

call curl -I "%REVOKE_AI_DEP%" -fs >.tmp.out
if %errorlevel% neq 0 (
    echo '!REVOKE_AI_VERSION!' is not a known branch name or tag. Please check the version and try again.
    echo "Press any key to continue"
    pause
    exit /b
)
del .tmp.out

echo This script will update RevokeAI and all its dependencies to !REVOKE_AI_SRC!.
echo If you do not want to do this, press control-C now!
pause

call curl -L "%REVOKE_AI_DEP%" > environments-and-requirements/requirements-base.txt
call curl -L "%REVOKE_AI_MODELS%" > configs/INITIAL_MODELS.yaml


call .venv\Scripts\activate.bat
call .venv\Scripts\python -mpip install -r requirements.txt
if %errorlevel% neq 0 (
   echo Installation of requirements failed. See https://revoke-ai.github.io/RevokeAI/installation/INSTALL_AUTOMATED/#troubleshooting for suggestions.
   pause
   exit /b
)

call .venv\Scripts\python -mpip install !REVOKE_AI_SRC!
if %errorlevel% neq 0 (
   echo Installation of RevokeAI failed. See https://revoke-ai.github.io/RevokeAI/installation/INSTALL_AUTOMATED/#troubleshooting for suggestions.
   pause
   exit /b
)

@rem call .venv\Scripts\revokeai-configure --root=.

@rem if %errorlevel% neq 0 (
@rem    echo Configuration RevokeAI failed. See https://revoke-ai.github.io/RevokeAI/installation/INSTALL_AUTOMATED/#troubleshooting for suggestions.
@rem    pause
@rem    exit /b
@rem )

echo RevokeAI has been updated to '%REVOKE_AI_VERSION%'

echo "Press any key to continue"
pause
endlocal
